pro mm_corr_stokesc1, m_tot, m_astron, stokesc1,azencoders,zaencoders,$
    m_rcvrcorr= m_rcvrcorr, m_skycorr= m_skycorr, m_astro= m_astro

;+
;NAME:
;mm_corr_stokesc1
;PURPOSE: mueller-correct the stokesc1 array IN PLACE. This is the primary
;data generated by CROSS3_GENCAL.

;CALLING SEQUENCE:
;
;   MM_CORR_STOKESC1, m_tot, m_astron, azmidpntn, zamidpntn,$
;   m_rcvrcorr= m_rcvrcorr, m_skycorr= m_skycorr, m_astro=m_astro

;
;INPUTS:

;       M_TOT, the mueller matrix for the electronics system as defined
;in the AOTM

;       M_ASTRON, the mueller matrix to convert to astronomy as defined
;in the AOTM
;
;   stokesc1[128,4,242] - intenisty and phase calibrated data
;   azencoders[60,4]    - az pos for non-cal data
;   zaencoders[60,4]    - za pos for non-cal data


;KEYWORD:
;
;   M_RCVRCORR: setting it includes the M_TOT correction.
;
;   M_SKYCORR, setting it includes the sky correction; default is zero.
;
;   M_ASTRO: setting includes the M_ASTRON correction
;
;OUTPUTS:
;
;   stokesc1: the corrected data.
;
;NOTE:
;   the stokesc1 data will be corrected according to the keywords
;   m_rcvrcorr,m_skycorr,m_astro
;   the data stokesc1[128,4,0:1] (the cals) will only be corrected for
;   m_rcvrcorr no matter what the other keywords are set to. This will
;   let you see how well the rcvr_cor is doing since all of the polarized
;   power should end up in U for the cal difference.
;   az,za encoders are 60,4 and are the positions for the strip data
;   (not including the cals)
;-

pa= (pangle( azencoders, zaencoders, 1))
pa= !dtor* pa

m_total= fltarr( 4,4, 240, /nozero)
m_cal  = fltarr( 4,4, 2, /nozero)

m_tot_used= fltarr( 4,4)
m_tot_used[ indgen(4) *5]= 1.

if ( keyword_set( m_rcvrcorr)) then m_tot_used= m_tot

m_tot= m_tot_used
for i=0,1 do m_cal[*,*,i]=m_tot

;print, 'M_TOT= ', m_tot

;CALCULATE M_SKY...IF DESIRED...
IF KEYWORD_SET( M_SKYCORR) THEN BEGIN
    m_sky= fltarr( 4,4, 240)
    m_sky[ 0,0, *]= 1.
    m_sky[ 3,3, *]= 1.
    m_sky[ 1,1, *]= cos( 2.* pa)
    m_sky[ 2,1, *]= sin( 2.* pa)
    m_sky[ 2,2, *]= m_sky[ 1,1, *]
    m_sky[ 1,2, *]= -m_sky[ 2,1, *]
;
;   no sky,astron correction to cal
;
    for nr=0, 239 do m_total[ *,*,nr]= m_tot ## m_sky[ *,*,nr]
ENDIF ELSE begin
    for nr=0, 239 do m_total[ *,*,nr]= m_tot
endelse
      
;DON'T FORGET TO TAKE THE INVERSE!!!
for nr=0, 239 do m_total[ *,*,nr]= invert( m_total[ *,*,nr])
for nr=0, 1   do m_cal[ *,*,nr]= invert( m_cal[ *,*,nr])

;THEN ROTATE TO ASTRONOMICAL PS IF DESIRED...
if keyword_set( m_astro) then $
for nr=0, 239 do m_total[ *,*,nr]= m_astron ## m_total[ *,*,nr]

;
; the cal are the first two, then the 240 strips
;
nchnl=128
;print,'stop in mm_corr_stokesc1 phil'
;stop
for nr=0, 1 do $
    stokesc1[*,*,nr]= m_cal[ *,*,nr] ## stokesc1[*,*,nr]

for nr=2,241 do $
    stokesc1[*,*,nr]= m_total[ *,*,nr-2] ## stokesc1[*,*,nr]

return
end
