;-----------------------------------------------------------------------------
;immosavg,{imday} hardcopy   - create mosaic of averages
;-----------------------------------------------------------------------------
pro immosavg,d,dohardcopy,_extra=e
;
;	parameters
;
	if  n_params() lt 2 then  dohardcopy = 0
	immossetup,d.yymmdd,1,dohardcopy,mos,_extra=e
	curwin = 0
	plotsleft=(size(d.frql))[1]			;number of frequencies
	pltperpage=mos.numrows*mos.numcols
;
;	yrange:
;
	ylfMax=0.
	ylfMin=-80.
	yhfMax=0.
	yhfMin=-60.
;
;	loop through all the plots
;
	for i=0,plotsleft-1 do  begin
	    curcol= ((i mod pltperpage) mod mos.numcols ) + 1	;1..numrows 
	    currow= ((i mod pltperpage) /   mos.numcols ) + 1	;1..numcols
;
;	 	new page, update window number, start new page
;
		if  (i mod pltperpage) eq 0 then begin
			if (dohardcopy eq 0)  then begin
  				curwin=curwin+1
  				window,curwin,ysize=mos.winypix
			endif
			!p.multi=[0,mos.numcols,mos.numrows,0,0]
		endif
;
		curfrq=d.frql[i]
		imgfrq,d,curfrq,dfrq		  ; select freq of interest
		imlin,dfrq				      ; convert db to linear
		imavg=imavg(dfrq)			  ; average
		imavg.d=(alog10(imavg.d))*10  ; back to db
;
;		left column yaxis labels, bottom row, xaxis labels
;
	    ytitle=''
	    xtitle=''
		if (curcol eq 1) then begin
			ytitle=mos.ytitle
		endif
		if (currow eq mos.numrows) then begin
			xtitle=mos.xtitle
		endif
;
;	yrange
;
		if      (curfrq lt  1400.) then ver,ylfMin,ylfMax      $
		else if (curfrq gt 1400.)  then ver,yhfMin,yhfMax $
		else ver,min(imavg.d),max(imavg.d)
;
   	    plot,immkfrq(imavg),imavg.d,xtitle=xtitle,ytitle=ytitle,xstyle=1
;
;		tsys label
;
		tsys=imtsys(curfrq) 
		if (tsys gt 0.) then begin
			lab=string(format='("Tsys:",f6.0)',tsys)
			xoff=!x.crange[1]
			yoff=!y.crange[1]+(!y.crange[1]-!y.crange[0])*.018
			xyouts,xoff,yoff,lab,alignment=1.,charsize=mos.charSizeTsys
		end
		if  (i mod pltperpage) eq 0 then begin
			xyouts,.5,mos.yOffMainTitle,mos.title,alignment=.5,/normal
		endif
		
	endfor
	if dohardcopy ne 0  then begin
		hardcopy
		if dohardcopy gt 1 then spawn,'lpr idl.ps'
		wait,3
		set_plot,'x'
	endif
	immosreset
	return
end
